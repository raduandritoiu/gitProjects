<?xml version="1.0" encoding="utf-8"?>
<s:GridItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009" 
					xmlns:s="library://ns.adobe.com/flex/spark" 
					xmlns:mx="library://ns.adobe.com/flex/mx" 
					width="60" height="20">
	
	<fx:Script>
		<![CDATA[
			import Swizzard.Data.Models.SiemensValve;
			import Swizzard.Data.Models.Enumeration.Valves.ValveMedium;
			import Swizzard.Data.Models.Enumeration.Valves.ValveType;
			import Swizzard.System.Mediators.Valves.ValveParametersFormMediator;
			import Swizzard.UI.Components.DataGridClasses.GridColumnsUtil;
			
			
			private var dataChanged:Boolean;
			
		
			override public function set data(value:Object):void {
				if (super.data == value) {
					return;
				}
				super.data = value;
				dataChanged = true;
				invalidateProperties();
			}
			
			
			override protected function commitProperties():void {
				super.commitProperties();
				if (dataChanged) {
					dataChanged = false;
					textLabel.text = valveActualDeltaPLabel(data);
				}
			}

			
			private function valveActualDeltaPLabel(item:Object):String {
				if (item == null)
					return "";
				
				var valve:SiemensValve = item as SiemensValve;
				if (valve.info.type == ValveType.PICV)
					return "N/A";
				
				var cv:Number = valve.info.cv;
				var pressureDrop:Number = 0;
				
				var formMediator:ValveParametersFormMediator = ValveParametersFormMediator.getInstance();
				switch (formMediator.selectedMedium) {
					//case ValveMedium.GLYCOL:
					case ValveMedium.WATER:
						pressureDrop = Math.pow(formMediator.requiredFlow / cv, 2);
						break;
					
					case ValveMedium.STEAM:
						pressureDrop = (Math.pow(formMediator.steamQuantity, 2) * formMediator.lastSpecificVolume) / (Math.pow(63.5, 2) * Math.pow(cv, 2));
						break;
				}
				
				if (!isNaN(pressureDrop))
					return GridColumnsUtil.numberFormatter.format(pressureDrop.toString());
				
				return "0";			
			}
		]]>
	</fx:Script>
	
	<s:Label id="textLabel" left="7" right="4" top="8" maxDisplayedLines="1"/>
	
</s:GridItemRenderer>
